//   .model Spectrum48
    DEVICE ZXSPECTRUM48
//    INCLUDE "TapLib.asm"

    DEFINE tape_file_name    "qloader_test.tap"
    DEFINE bin_file_name    "qloader_test.bin"
    DEFINE basic_name "qloadertst"
start:
    ld DE, datablock - start    // length
    ADD HL, DE                  // When called from qloader HL contains PC at start
    ld B, (HL)
    inc HL
    ld C, (HL)                  // BC is now length
    inc HL

    ld A, (HL)
    ld IXH, A
    inc HL
    ld A, (HL)
    ld IXL, A        // IX is now polynome
    inc HL

    //LD B, IXH       //  @DEBUG
    //LD C, IXL       //  @DEBUG     return poly to BASIC
    //ret
  

    ld D, H         // DE is now data ptr
    ld E, L

   // ld B,(HL)           //  @DEBUG return first 2 bytes to BASIC
   // inc HL      
   // ld C,(HL)
   // ret
    
 
CRC16:
    ld HL, 0xFFFF // init value
.byte_loop    
    push BC
    ld A,(DE)
    inc DE
    xor H
    ld H, A
    ld B, 8         // 8 bits
.bit_loop:    
    add HL, HL      // HL << 1
    jr nc,    .skip_xor

    ld A, IXH
    xor H
    ld H,A
    ld A, IXL
    xor L
    ld L,A

.skip_xor    
    djnz .bit_loop
    pop BC
    dec BC
    ld A, B
    OR C
    jr nz, .byte_loop
    
    // Return CRC to basic
    ld B, H
    ld C, L
    ret

datablock:
length EQU  datablock
poly EQU   datablock+2
byte:       // more test bytes follow here
end:      


 DISPLAY "start = ",/A,start   
 DISPLAY "datablock = ",/A,datablock   
// ===========================================================================
    DISPLAY "-- Creating file: ", tape_file_name
    EMPTYTAP tape_file_name
    // SAVETAP <filename>,    BASIC,<filenameintapeheader>,<start>,     <length> [,<autorunline>[,<lengthwithoutvars>]]
    SAVETAP    tape_file_name,CODE,basic_name,        start, end-start
 
// ===========================================================================

    DISPLAY "-- Creating file: ", bin_file_name, " with length = ",/A, end-start
    SAVEBIN bin_file_name, start, end-start

